/*
 * Test a particular conjugacy.
 *
 * - m: Model.
 * - N: Number of samples.
 * - D: Number of dimensions.
 * - lazy: Use lazy version?
 */
function test_conjugacy<Conjugate>(m:Conjugate, N:Integer, D:Integer, lazy:Boolean) {  
  forward:Handler <- PlayHandler(false);
  backward:Handler <- PlayHandler(true);
  if lazy {
    forward <- MoveHandler(false);
    backward <- MoveHandler(true);
  }
  m.initialize();
  
  /* simulate forward */
  X1:Real[N,D];
  for n in 1..N {
    let m' <- clone(m);
    with (forward) {
      m'.simulate();
    }
    X1[n,1..D] <- m'.forward();
    if mod(n, 10000) == 0 {
      collect();
    }
  }

  /* simulate backward */
  X2:Real[N,D];
  for n in 1..N {
    let m' <- clone(m);
    with (backward) {
      m'.simulate();
    }
    X2[n,1..D] <- m'.backward();
    if mod(n, 10000) == 0 {
      collect();
    }
  }
  
  /* test result */
  if !pass(X1, X2) {
    exit(1);
  }
}
