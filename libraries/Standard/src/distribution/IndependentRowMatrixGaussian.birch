/**
 * Matrix Gaussian distribution where each row is independent.
 */
final class IndependentRowMatrixGaussian(M:Expression<Real[_,_]>,
    V:Expression<LLT>) < Distribution<Real[_,_]> {
  /**
   * Mean.
   */
  M:Expression<Real[_,_]> <- M;
  
  /**
   * Among-column covariance.
   */
  V:Expression<LLT> <- V;

  function rows() -> Integer {
    return M.rows();
  }

  function columns() -> Integer {
    return M.columns();
  }

  function supportsLazy() -> Boolean {
    return true;
  }

  function simulate() -> Real[_,_] {
    return simulate_matrix_gaussian(M.value(), V.value());
  }
  
  function simulateLazy() -> Real[_,_]? {
    return simulate_matrix_gaussian(M.get(), V.get());
  }
  
  function logpdf(x:Real[_,_]) -> Real {
    return logpdf_matrix_gaussian(x, M.value(), V.value());
  }

  function logpdfLazy(x:Expression<Real[_,_]>) -> Expression<Real>? {
    return logpdf_lazy_matrix_gaussian(x, M, V);
  }

  function graft() -> Distribution<Real[_,_]> {
    prune();
    s1:InverseWishart?;
    m1:TransformLinearMatrix<MatrixNormalInverseWishart>?;
    m2:MatrixNormalInverseWishart?;
    r:Distribution<Real[_,_]> <- this;

    /* match a template */
    let compare <- V.distribution();
    if compare? && (m1 <- M.graftLinearMatrixNormalInverseWishart(compare!))? {
      r <- LinearMatrixNormalInverseWishartMatrixGaussian(m1!.A, m1!.X, m1!.C);
    } else if compare? && (m2 <- M.graftMatrixNormalInverseWishart(compare!))? {
      r <- MatrixNormalInverseWishartMatrixGaussian(m2!);
    } else if (s1 <- V.graftInverseWishart())? {
      r <- MatrixNormalInverseWishart(M, box(llt(identity(M.rows()))), s1!);
    }

    return r;
  }

  function graftMatrixGaussian() -> MatrixGaussian? {
    prune();
    return Gaussian(M, box(llt(identity(M.rows()))), V);
  }

  function graftMatrixNormalInverseWishart(compare:Distribution<LLT>) ->
      MatrixNormalInverseWishart? {
    prune();
    s1:InverseWishart?;
    r:MatrixNormalInverseWishart?;
    
    /* match a template */
    if (s1 <- V.graftInverseWishart())? && s1! == compare {
      r <- MatrixNormalInverseWishart(M, box(llt(identity(M.rows()))), s1!);
    }

    return r;
  }
}

/**
 * Create matrix Gaussian distribution where each row is independent.
 */
function Gaussian(M:Expression<Real[_,_]>, V:Expression<LLT>) ->
    IndependentRowMatrixGaussian {
  return construct<IndependentRowMatrixGaussian>(M, V);
}

/**
 * Create matrix Gaussian distribution where each row is independent.
 */
function Gaussian(M:Expression<Real[_,_]>, V:LLT) ->
    IndependentRowMatrixGaussian {
  return Gaussian(M, box(V));
}

/**
 * Create matrix Gaussian distribution where each row is independent.
 */
function Gaussian(M:Real[_,_], V:Expression<LLT>) ->
    IndependentRowMatrixGaussian {
  return Gaussian(box(M), V);
}

/**
 * Create matrix Gaussian distribution where each row is independent.
 */
function Gaussian(M:Expression<Real[_,_]>, V:Expression<Real[_,_]>) ->
    IndependentRowMatrixGaussian {
  return Gaussian(M, llt(V));
}

/**
 * Create matrix Gaussian distribution where each row is independent.
 */
function Gaussian(M:Expression<Real[_,_]>, V:Real[_,_]) ->
    IndependentRowMatrixGaussian {
  return Gaussian(M, llt(V));
}

/**
 * Create matrix Gaussian distribution where each row is independent.
 */
function Gaussian(M:Real[_,_], V:Expression<Real[_,_]>) ->
    IndependentRowMatrixGaussian {
  return Gaussian(M, llt(V));
}
