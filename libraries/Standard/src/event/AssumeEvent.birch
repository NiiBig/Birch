/**
 * *Assume* event triggered during the execution of a model, typically by
 * the `~` operator.
 *
 * - x: Associated random variate.
 * - p: Associated distribution.
 *
 * ```mermaid
 * classDiagram
 *    Event <|-- SimulateEvent
 *    Event <|-- ObserveEvent
 *    Event <|-- AssumeEvent
 *    Event <|-- FactorEvent
 *    link Event "../Event/"
 *    link SimulateEvent "../SimulateEvent/"
 *    link ObserveEvent "../ObserveEvent/"
 *    link AssumeEvent "../AssumeEvent/"
 *    link FactorEvent "../FactorEvent/"
 * ```
 */
final class AssumeEvent<Value>(x:Random<Value>, p:Distribution<Value>) <
    Event {
  /**
   * Random variate.
   */
  x:Random<Value> <- x;
  
  /**
   * Distribution.
   */
  p:Distribution<Value> <- p;

  function record() -> Record {
    return AssumeRecord(x);
  }
  
  function coerce(record:Record) -> AssumeRecord<Value> {
    let r <- AssumeRecord<Value>?(record);
    if !r? {
      error("incompatible trace");
    }
    return r!;
  }

  function accept(handler:PlayHandler) {
    handler.doHandle(this);
  }
  
  function accept(handler:MoveHandler) {
    handler.doHandle(this);
  }

  function accept(record:Record, handler:PlayHandler) {
    handler.doHandle(coerce(record), this);
  }

  function accept(record:Record, handler:MoveHandler) {
    handler.doHandle(coerce(record), this);
  }
}

/**
 * Create an AssumeEvent.
 */
function AssumeEvent<Value>(x:Random<Value>, p:Distribution<Value>) ->
    AssumeEvent<Value> {
  return construct<AssumeEvent<Value>>(x, p);
}
