/**
 * Multiple object model.
 */
class Multi < StateSpaceModel<Global,Array<Track>,Array<Random<Real[_]>>> {
  /**
   * Current time.
   */
  t:Integer <- 0;

  /**
   * All tracks up to current time.
   */
  z:Array<Track>;
  
  function transition(x':Array<Track>, x:Array<Track>, θ:Global) {
    /* update time */
    t <- t + 1;

    /* move current objects */
    let iter <- x.walk();
    while iter.hasNext() {
      let track <- iter.next();
      let ρ <- exp(logpdf_poisson(t - track.t - 1, θ.τ));
      let R <- 1.0 - cdf_poisson(t - track.t - 1, θ.τ) + ρ;
      s:Boolean;
      s <~ Bernoulli(1.0 - ρ/R);  // does the object survive?
      if s {
        track.simulate(t - track.t + 1);
        x'.pushBack(track);
      }
    }
    
    /* birth new objects */
    N:Integer;
    N <~ Poisson(θ.λ);
    for n in 1..N {
      track:Track;
      track.t <- t;
      track.θ <- θ;
      track.simulate();   // up to parameters
      track.simulate(1);  // up to initial time
      x'.pushBack(track);
      z.pushBack(track);
    }
  }

  function observation(y:Array<Random<Real[_]>>, x:Array<Track>, θ:Global) {
    if !y.empty() {
      association(y, x, θ);
    } else {
      /* clutter */
      N:Integer;
      N <~ Poisson(θ.μ);
      for n in 1..(N + 1) {
        clutter:Random<Real[_]>;
        clutter <~ Uniform(θ.l, θ.u);
        y.pushBack(clutter);
      }
    }
  }

  function association(y:Array<Random<Real[_]>>, x:Array<Track>, θ:Global) {
    let iter <- x.walk();
    while iter.hasNext() {
      let track <- iter.next();
      let o <- track.y.back();  // observed random variable
      if o.hasDistribution() {
        let p <- o.distribution()!;
      
        /* object is detected, compute proposal */
        q:Real[y.size()];
        let n <- 1;
        let detection <- y.walk();
        while detection.hasNext() {
          q[n] <- p.pdf(detection.next().value());
          n <- n + 1;
        }
        let Q <- sum(q);
                  
        /* propose an association */
        if Q > 0.0 {
          q <- q/Q;
          n <~ Categorical(q);  // propose an observation to associate with
          let w <- p.observe(y.get(n).value());  // likelihood
          w <- w - log(Real(y.size()));  // prior correction (uniform prior)
          w <- w - log(q[n]);  // proposal correction
          y.erase(n);  // remove the observation for future associations
          factor w;
        } else {
          factor -inf;  // detected, but all likelihoods (numerically) zero
        }
      }
    }
    
    /* clutter */
    y.size() - 1 ~> Poisson(θ.μ);
    let clutter <- y.walk();
    while clutter.hasNext() {
      clutter.next().value() ~> Uniform(θ.l, θ.u);
    }
  }
    
  function write(buffer:Buffer) {
    buffer.set("θ", θ);
    buffer.set("z", z);
    buffer.set("y", y);
  }
}
